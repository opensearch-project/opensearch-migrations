plugins {
    id 'jacoco'
    id 'org.owasp.dependencycheck' version '8.2.1'
}

repositories {
    mavenCentral()
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'org.owasp.dependencycheck'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(11)
        }
    }

    jacoco {
        toolVersion = '0.8.9'
    }

    test {
        // Provide way to exclude particular tests from CLI
        // e.g. ./gradlew test -PexcludeTests=**/KafkaProtobufConsumerLongTermTest*
        if (project.hasProperty('excludeTests')) {
            exclude project.property('excludeTests')
        }
        systemProperty 'disableMemoryLeakTests', 'true'
        useJUnitPlatform {
            excludeTags 'longTest'
        }
        jvmArgs '-ea'

        jacoco {
            enabled = false
        }
    }

    task slowTest(type: Test) {
        dependsOn test
        // Provide way to exclude particular tests from CLI
        // e.g. ./gradlew test -PexcludeTests=**/KafkaProtobufConsumerLongTermTest*
        if (project.hasProperty('excludeTests')) {
            exclude project.property('excludeTests')
        }
        systemProperty 'disableMemoryLeakTests', 'false'
        useJUnitPlatform {
        }
        jvmArgs '-ea'

        jacoco {
            enabled = true
        }
    }

    // Utility task to allow copying required libraries into a 'dependencies' folder for security scanning
    tasks.register('copyDependencies', Copy) {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE

        from configurations.runtimeClasspath
        into "${buildDir}/dependencies"
    }
}

jacocoTestReport {
    dependsOn subprojects*.slowTest
    additionalSourceDirs.from(files(subprojects.sourceSets.main.allSource.srcDirs))
    sourceDirectories.from(files(subprojects.sourceSets.main.allSource.srcDirs))
    classDirectories.from(files(subprojects.sourceSets.main.output))
    executionData.from(subprojects.collect {
        "${it.buildDir}/jacoco/slowTest.exec"
    })

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/protos/**', '**/JMeterLoadTest**'])
        }))
    }

    reports {
        println("Builddir=${buildDir}...${classDirectories}")
        xml.required = true
        xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
        html.required = true
        html.destination file("${buildDir}/reports/jacoco/test/html")
    }
}