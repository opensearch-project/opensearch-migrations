import org.opensearch.migrations.common.CommonUtils
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.github.gradle.node.npm.task.NpmTask

plugins {
    id 'org.opensearch.migrations.java-library-conventions'
    id "com.avast.gradle.docker-compose"
    id 'com.bmuschko.docker-remote-api'
    id 'com.github.node-gradle.node' version '7.1.0'
}

node {
    version = '22.14.0'
    download = true
}

// Define paths at the top level
def orchestrationSpecsDirP = rootProject.layout.projectDirectory.dir("orchestrationSpecs")
def orchestrationP     = orchestrationSpecsDirP
def configProcessorP   = orchestrationP.dir("packages/config-processor")
def workflowTemplatesP = orchestrationP.dir("packages/migration-workflow-templates")
def configProcessorStagingP =
        layout.buildDirectory.dir(CommonUtils.getDockerBuildDirName("migration_console", "migrationConsole")+"/nodeStaging/configProcessor")
def workflowTemplatesStagingP =
        layout.buildDirectory.dir(CommonUtils.getDockerBuildDirName("migration_console", "migrationConsole")+"/nodeStaging/workflowTemplates")

tasks.register('installDependencies', NpmTask) {
    description = 'Install npm dependencies for orchestrationSpecs'
    workingDir = orchestrationSpecsDirP.asFile
    args = ['ci', '--force', '--omit=optional']

    inputs.file(orchestrationSpecsDirP.file("package.json"))
    inputs.file(orchestrationSpecsDirP.file("package-lock.json"))
    outputs.dir(orchestrationSpecsDirP.dir("node_modules"))
}

tasks.register('testWorkflowTemplates', NpmTask) {
    description = 'Run Jest tests for workflow-templates'
    workingDir = workflowTemplatesP.asFile
    args = ['run', 'test-all']
    dependsOn installDependencies

    inputs.dir(workflowTemplatesP.dir("src"))
    inputs.file(workflowTemplatesP.file("jest.config.js")).optional()
    outputs.dir(workflowTemplatesP.dir("coverage")).optional()
}

tasks.register('makeAndStageWorkflowTemplates', NpmTask) {
    description = 'Build the workflow templates from their TypeScript definitions'
    workingDir = workflowTemplatesP.asFile
    args = ['run', 'check-and-make-templates', '--', '--outputDirectory', workflowTemplatesStagingP.get().asFile.absolutePath]
    dependsOn installDependencies
    dependsOn testWorkflowTemplates

    inputs.dir(workflowTemplatesP.dir("src"))
    outputs.dir(workflowTemplatesStagingP)

    doFirst {
        workflowTemplatesStagingP.get().asFile.mkdirs()
    }
}

tasks.register('typeCheckConfigProcessor', NpmTask) {
    description = 'Run TypeScript type checking for config-processor'
    workingDir = configProcessorP.asFile
    args = ['run', 'type-check']
    dependsOn installDependencies

    inputs.dir(configProcessorP.dir("src"))
    inputs.dir(configProcessorP.dir("scripts/createMigrationWorkflowFromUserConfiguration.sh"))
    inputs.file(configProcessorP.file("tsconfig.json"))
    outputs.upToDateWhen { false }
}

tasks.register('testConfigProcessor', NpmTask) {
    description = 'Run Jest tests for config-processor'
    workingDir = configProcessorP.asFile
    args = ['run', 'test']
    dependsOn installDependencies

    inputs.dir(configProcessorP.dir("src"))
    inputs.file(configProcessorP.file("jest.config.js")).optional()
    outputs.dir(configProcessorP.dir("coverage")).optional()
}

tasks.register('buildAndStageConfigProcessor', NpmTask) {
    description = 'Build and stage the config-processor TypeScript package directly to staging'
    workingDir = configProcessorP.asFile
    args = ['run', 'bundle', '--', configProcessorStagingP.get().asFile.absolutePath]
    dependsOn installDependencies
    dependsOn testConfigProcessor

    inputs.dir(configProcessorP.dir("src"))
    inputs.file(configProcessorP.file("makeBundle.js"))
    outputs.dir(configProcessorStagingP)

    doFirst {
        configProcessorStagingP.get().asFile.mkdirs()
    }
}

ext.directorySize = { File dir ->
    if (!dir.exists()) return 0L
    return dir.directorySize()
}

def dockerFilesForExternalServices = [
        "elasticsearch_searchguard": "elasticsearchWithSearchGuard",
        "capture_proxy_base": "captureProxyBase",
        "k8s_config_map_util_scripts": "k8sConfigMapUtilScripts",
        "elasticsearch_client_test_console": "elasticsearchTestConsole",
        "migration_console": "migrationConsole",
        "otel_collector": "otelCollector",
        "grafana": "grafana"
]

dockerFilesForExternalServices.each { dockerImageName, projectName ->
    task("buildDockerImage_${dockerImageName}", type: DockerBuildImage) {
        if (projectName == "migrationConsole") {
            dependsOn makeAndStageWorkflowTemplates
            dependsOn buildAndStageConfigProcessor

            def libraries = [
                    project(":libraries:kafkaCommandLineFormatter")
            ]

            def applications = [
                    project(":CreateSnapshot"),
                    project(":MetadataMigration")
            ]
            // Setup additional sync task which doesn't depend on the base image being built, for other projects to use
            getMigrationConsoleSyncTask(project, dockerImageName, projectName, libraries, applications, false)
            def syncTask = getMigrationConsoleSyncTask(project, dockerImageName, projectName, libraries, applications, true)

            dependsOn syncTask
            inputDir = syncTask.destinationDir
        } else {
            inputDir = project.file("src/main/docker/${projectName}")
        }
        def hashNonce = CommonUtils.calculateDockerHash(project.fileTree(inputDir))
        images.add("migrations/${dockerImageName}:${hashNonce}".toString())
        images.add("migrations/${dockerImageName}:latest".toString())
    }
}

static Sync getMigrationConsoleSyncTask(Project project, String dockerImageName, String projectName, List<Project> libraries, List<Project> applications, boolean dependOnBaseImage) {
    // Create a single sync task to copy the required files
    def destDir = "build/docker/${dockerImageName}_${projectName}"
    def taskName = "syncArtifact_${dockerImageName}_${projectName}"
    if (!dependOnBaseImage) {
        taskName= "${taskName}_noDockerBuild"
    }
    def syncTask = project.tasks.create(taskName, Sync) {
        into destDir
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE

        preserve {
            include 'nodeStaging/**'
        }

        // Applications and Standalone Libraries both have libraries, sync them
        (libraries + applications).each { lib ->
            def applicationDestDir = "staging/${lib.name}/"

            // Ensure dependencies are resolved within the correct project context
            project.evaluationDependsOn(lib.path)

            from (lib.files { lib.configurations.runtimeClasspath }) {
                into "${applicationDestDir}/lib"
            }
            from (lib.tasks.named('jar')) {
                into "${applicationDestDir}/lib"
            }
        }

        // Sync application start scripts
        applications.each { app ->
            def applicationDestDir = "staging/${app.name}/"
            from (app.tasks.named('startScripts').get().outputs.files) {
                into "${applicationDestDir}/bin"
            }
        }

        // Sync VERSION file
        from (project.rootProject.layout.projectDirectory.file("VERSION"))

        from(project.layout.projectDirectory.dir("src/main/docker/${projectName}").asFileTree.matching {
            exclude("**/build/**", "**/.venv/**")
        })
    }

    // Collect assemble tasks from libraries and applications
    def assembleTasks = (libraries + applications).collect { it.tasks.named("assemble") }
    // Ensure the sync task depends on the assemble tasks
    syncTask.dependsOn assembleTasks

    if (dependOnBaseImage) {
        // Migration Console base image is the test console
        syncTask.dependsOn "buildDockerImage_elasticsearch_client_test_console"
    }

    return syncTask
}

def javaContainerServices = [
        "capture_proxy": ":TrafficCapture:trafficCaptureProxyServer",
        "capture_proxy_es": ":TrafficCapture:trafficCaptureProxyServer",
        "traffic_replayer": ":TrafficCapture:trafficReplayer"
]
def baseImageOverrides = [
        "capture_proxy": "capture_proxy_base",
        "capture_proxy_es": "elasticsearch_searchguard",
]
javaContainerServices.each { dockerImageName, projectName ->
    def artifactProject = project(projectName);
    CommonUtils.copyArtifactFromProjectToProjectsDockerStaging(project as Project, artifactProject, dockerImageName)
    CommonUtils.createDockerfile(project, artifactProject, baseImageOverrides[dockerImageName], dockerFilesForExternalServices, dockerImageName)
}

javaContainerServices.forEach { dockerImageName, gradleProjectName ->
    def projectName = project(gradleProjectName).name;
    def dockerBuildDir = "build/docker/${dockerImageName}_${projectName}"
    task "buildDockerImage_${dockerImageName}"(type: DockerBuildImage) {
        dependsOn "createDockerfile_${dockerImageName}"
        inputDir = project.file("${dockerBuildDir}")
        images.add("migrations/${dockerImageName}:${version}".toString())
        images.add("migrations/${dockerImageName}:latest".toString())
    }
}

dockerCompose {
    def extensionsDir = "src/main/docker/composeExtensions/"
    useComposeFiles =
            ['src/main/docker/docker-compose.yml',
             "${extensionsDir}/" + (project.hasProperty("otel-collector") ?
                     "${project.getProperty('otel-collector')}" :
                     "otel-prometheus-jaeger.yml"),
             "${extensionsDir}" + (project.hasProperty("multiProxy") ? "proxy-multi.yml" : "proxy-single.yml")
            ]
}

task buildDockerImages {
    dependsOn buildDockerImage_k8s_config_map_util_scripts
    dependsOn buildDockerImage_elasticsearch_searchguard
    dependsOn buildDockerImage_migration_console
    dependsOn buildDockerImage_otel_collector
    dependsOn buildDockerImage_grafana
    dependsOn buildDockerImage_traffic_replayer
    dependsOn buildDockerImage_capture_proxy
    dependsOn buildDockerImage_capture_proxy_es
}

tasks.getByName('composeUp')
        .dependsOn(tasks.getByName('buildDockerImages'))
