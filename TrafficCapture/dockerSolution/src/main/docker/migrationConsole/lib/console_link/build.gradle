plugins {
    id 'base'
    id 'de.undercouch.download' version '5.5.0'
}

def apiPort = 8099 // Not a commonly used
def openapiUrl = "http://127.0.0.1:${apiPort}/openapi.json"
def serverPidFile = "${buildDir}/fastapi.pid"
def openapiOutFile = "${buildDir}/openapi.json"

def pythonSources = fileTree(dir: projectDir, include: '**/*.py')
def dependencyFiles = files(
    "Pipfile",
    "Pipfile.lock"
).filter { it.exists() }
def projectDirRef = projectDir

tasks.register('startApiServer') {
    doLast {
        def command = "pipenv run gunicorn console_link.api.main:app -k uvicorn.workers.UvicornWorker -w 1 -b 127.0.0.1:${apiPort} --error-logfile -"
        def proc = command.execute(null, projectDirRef)
        sleep 2000

        logger.lifecycle("Server started with ${command}, pid: ${proc.pid()}")
        new File(serverPidFile).text = "${proc.pid()}"
    }
}

tasks.register('downloadOpenApiSpec', de.undercouch.gradle.tasks.download.Download) {
    src openapiUrl
    dest openapiOutFile
    dependsOn 'startApiServer'

    inputs.files(pythonSources)
    inputs.files(dependencyFiles)
    outputs.file(openapiOutFile)
}

tasks.register('stopApiServer') {
    doLast {
        def pidFile = new File(serverPidFile)
        if (pidFile.exists()) {
            def pid = pidFile.text.trim()
            logger.info("Killing FastAPI server with PID $pid")
            ["kill", pid].execute().waitFor()
            pidFile.delete()
        }
    }
    inputs.files(serverPidFile)
}

tasks.register('fetchOpenApiSpec') {
    dependsOn 'downloadOpenApiSpec'
    finalizedBy 'stopApiServer'
    doLast {
        logger.lifecycle("OpenAPI spec downloaded to ${openapiOutFile}")
    }
}
