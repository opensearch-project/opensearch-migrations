import com.github.gradle.node.npm.task.NpmTask

plugins {
    id 'org.opensearch.migrations.java-application-conventions'
    id 'io.freefair.lombok'
    id 'com.google.cloud.tools.jib'
    id 'com.github.node-gradle.node' version '7.1.0'
}

node {
    version = '22.14.0'
    download = true
    nodeProjectDir = file("${projectDir}/transforms")
    workDir = file("${project.buildDir}/nodejs")
    npmWorkDir = file("${project.buildDir}/npm")
}

tasks.register('installTransformDeps', NpmTask) {
    description = 'Install TypeScript transform dependencies'
    args = ['install']
}

tasks.register('buildTransforms', NpmTask) {
    description = 'Build TypeScript transforms to GraalVM-compatible JS'
    args = ['run', 'build']
    dependsOn installTransformDeps
    inputs.dir("${projectDir}/transforms/src")
    outputs.dir("${projectDir}/transforms/dist")
}

tasks.register('copyTransforms', Copy) {
    description = 'Copy built JS transforms to test resources'
    from "${projectDir}/transforms/dist"
    into "${buildDir}/resources/test/transforms"
    dependsOn buildTransforms
}

processTestResources.dependsOn copyTransforms

dependencies {
    implementation project(':transformation:transformationPlugins:jsonMessageTransformers:jsonMessageTransformerInterface')
    implementation project(':transformation:transformationPlugins:jsonMessageTransformers:jsonJSTransformer')
    implementation project(':transformation:transformationPlugins:jsonMessageTransformers:jsonMessageTransformerLoaders')
    implementation project(':awsUtilities')

    implementation libs.netty.all
    implementation libs.jackson.databind
    implementation libs.jcommander
    implementation libs.slf4j.api
    implementation libs.log4j.api
    implementation libs.log4j.core
    implementation libs.log4j.slf4j2.impl
    implementation libs.aws.auth
    implementation libs.aws.sdk.core

    runtimeOnly project(':transformation:transformationPlugins:jsonMessageTransformers:jsonJSTransformerProvider')

    testImplementation libs.junit.jupiter.api
    testImplementation libs.junit.jupiter.params
    testImplementation libs.testcontainers
    testImplementation libs.testcontainers.junit.jupiter
    testImplementation libs.testcontainers.opensearch
    testRuntimeOnly libs.junit.jupiter.engine
}

application {
    mainClass = 'org.opensearch.migrations.transform.shim.TransformationShimMain'
}

jib {
    to {
        image = 'migrations/transformation_shim'
    }
    from {
        image = 'amazoncorretto:17-al2023-headless'
    }
}

tasks.withType(Test).configureEach {
    systemProperty 'io.netty.handler.codec.http.defaultStrictLineParsing', false
}
