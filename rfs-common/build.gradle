plugins {
    id 'org.opensearch.migrations.java-library-conventions'
    id 'io.freefair.lombok'
    id 'com.gradleup.shadow'
}

group = 'org.opensearch.migrations.snapshots'

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                description = 'Shared IR types and interfaces for OpenSearch snapshot reading and migration'
            }
        }
    }
}

configurations {
    lucene9
}

tasks.register('shadowLucene9', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    mergeServiceFiles()
    configurations = [project.configurations.lucene9]
    archiveClassifier.set('lucene9-shadow')
    relocate 'org.apache.lucene', 'shadow.lucene9.org.apache.lucene'
}

tasks.named('build') {
    dependsOn shadowLucene9
}

tasks.named('compileJava') {
    dependsOn shadowLucene9
}

dependencies {
    implementation project(':transformation')
    implementation project(':awsUtilities')
    implementation project(':coreUtilities')

    implementation libs.jackson.databind
    implementation libs.jackson.core
    implementation libs.jackson.annotations
    implementation libs.jackson.dataformat.smile

    implementation libs.jcommander
    implementation libs.slf4j.api

    // HTTP infrastructure (RestClient, ConnectionContext, etc.)
    implementation libs.netty.codec.http
    implementation libs.reactor.netty.core
    implementation libs.reactor.netty.http
    implementation libs.aws.auth

    lucene9 libs.lucene.v9.core
    implementation shadowLucene9.outputs.files
}
