plugins {
    id 'org.opensearch.migrations.java-library-conventions'
    id 'io.freefair.lombok'
    id 'com.gradleup.shadow'
}

group = 'org.opensearch.migrations.snapshots'

sourceSets {
    lucene9 {
        java {
            srcDir 'src/lucene9/java'
        }
        resources {
            srcDir 'src/lucene9/resources'
        }
    }
}

configurations {
    lucene5
    lucene6
    lucene7
    lucene9
    lucene9Implementation.extendsFrom(lucene9)
}

tasks.named('processLucene9Resources').configure {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

tasks.register('shadowLucene5', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    mergeServiceFiles()
    configurations = [project.configurations.lucene5]
    archiveClassifier.set('lucene5-shadow')
    relocate 'org.apache.lucene', 'shadow.lucene5.org.apache.lucene'
}

tasks.register('shadowLucene6', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    mergeServiceFiles()
    configurations = [project.configurations.lucene6]
    archiveClassifier.set('lucene6-shadow')
    relocate 'org.apache.lucene', 'shadow.lucene6.org.apache.lucene'
}

tasks.register('shadowLucene7', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    mergeServiceFiles()
    configurations = [project.configurations.lucene7]
    archiveClassifier.set('lucene7-shadow')
    relocate 'org.apache.lucene', 'shadow.lucene7.org.apache.lucene'
}

tasks.register('shadowLucene9', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    dependsOn compileLucene9Java, processLucene9Resources
    mergeServiceFiles()
    archiveClassifier.set('lucene9-shadow')
    configurations = [project.configurations.lucene9]
    from sourceSets.lucene9.output
    relocate 'org.apache.lucene', 'shadow.lucene9.org.apache.lucene'
    manifest {
        attributes 'Multi-Release': 'true'
    }
}

tasks.register('prepareShadowJars') {
    dependsOn shadowLucene5, shadowLucene6, shadowLucene7, shadowLucene9
}

tasks.named('build') {
    dependsOn prepareShadowJars
}

tasks.named('test') {
    dependsOn prepareShadowJars
}

// Publish shadow jars as additional artifacts
publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                description = 'Reads Elasticsearch/OpenSearch snapshots and extracts documents and metadata via Lucene'
            }
            ['shadowLucene5', 'shadowLucene6', 'shadowLucene7', 'shadowLucene9'].each { taskName ->
                artifact(tasks.named(taskName))
            }
        }
    }
}

dependencies {
    api project(':rfs-common')

    implementation libs.slf4j.api
    implementation libs.jackson.databind
    implementation libs.jackson.dataformat.smile
    implementation libs.zstd.jni
    implementation libs.reactor.core

    testImplementation libs.mockito.core
    testImplementation libs.mockito.junit.jupiter

    lucene5 libs.lucene.v5.core
    lucene5 libs.lucene.v5.backward.codecs
    lucene5 libs.lucene.v5.suggest

    lucene6 libs.lucene.v6.core
    lucene6 libs.lucene.v6.backward.codecs
    lucene6 libs.lucene.v6.suggest

    lucene7 libs.lucene.v7.core
    lucene7 libs.lucene.v7.backward.codecs
    lucene7 libs.lucene.v7.suggest

    lucene9 libs.lucene.v9.core
    lucene9 libs.lucene.v9.analysis.common
    lucene9 libs.lucene.v9.backward.codecs
    lucene9 libs.lucene.v9.suggest
    lucene9 libs.zstd.jni

    implementation shadowLucene5.outputs.files
    implementation shadowLucene6.outputs.files
    implementation shadowLucene7.outputs.files
    implementation shadowLucene9.outputs.files
}
