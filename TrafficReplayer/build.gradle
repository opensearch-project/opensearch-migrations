/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * The OpenSearch Contributors require contributions made to
 * this file be licensed under the Apache-2.0 license or a
 * compatible open source license.
 *
 * Modifications Copyright OpenSearch Contributors. See
 * GitHub history for details.
 */


buildscript {
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.1'
    }
}

plugins {
    id 'java'
    id 'application'
    id "com.github.spotbugs" version "4.7.3"
    id 'checkstyle'
}

spotbugs {
    includeFilter = file('config/spotbugs/spotbugs-include.xml')
}

checkstyle {
    toolVersion = '10.9.3'
    configFile = new File(rootDir, 'config/checkstyle/checkstyle.xml')
    System.setProperty('checkstyle.cache.file', String.format('%s/%s',
            buildDir, 'checkstyle.cachefile'))
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'io.netty', name: 'netty-all', version: '4.1.89.Final'
    implementation group: 'org.json', name: 'json', version: '20220924'
    implementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.8.1'
    implementation group: 'com.google.guava', name: 'guava', version: '31.1-jre'
    implementation group: 'org.projectlombok', name: 'lombok', version: '1.18.22'
    implementation group: 'com.beust', name: 'jcommander', version: '1.82'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.20.0'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.20.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
    annotationProcessor 'org.projectlombok:lombok:1.18.26'

}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
    }

    test {
        java {
            srcDir file('tst')
        }
    }
}

application {
    mainClass = 'org.opensearch.migrations.replay.TrafficReplayer'
}

jar {
    manifest {
        attributes 'Main-Class': application.mainClass
    }
}

task uberJar(type: Jar) {
    manifest {
        attributes 'Main-Class': application.mainClass
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    archiveBaseName = project.name + "-uber"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    with jar
}

tasks.named('test') {
    useJUnitPlatform()
}
