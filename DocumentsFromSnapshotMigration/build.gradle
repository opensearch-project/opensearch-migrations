plugins {
    id 'org.opensearch.migrations.java-application-conventions'
    id 'io.freefair.lombok'
    id 'com.avast.gradle.docker-compose'
    id 'com.bmuschko.docker-remote-api'
}

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import groovy.transform.Canonical
import org.opensearch.migrations.common.CommonUtils

@Canonical
class DockerServiceProps {
    String projectName = ""
    String dockerImageName = ""
    String inputDir = ""
    Map<String, String> buildArgs = [:]
    List<String> taskDependencies = []
}

dependencies {
    implementation project(":coreUtilities")
    implementation project(":RFS")
    implementation project(":transformation")
    implementation project(':transformation:transformationPlugins:jsonMessageTransformers:jsonMessageTransformerLoaders')
    runtimeOnly project(':transformation:transformationPlugins:jsonMessageTransformers:jsonTypeMappingsSanitizationTransformerProvider')

    implementation libs.log4j.api
    implementation libs.log4j.core
    implementation libs.jcommander
    implementation libs.jackson.databind
    implementation libs.jackson.annotations
    implementation libs.jackson.core
    implementation libs.jackson.dataformat.smile
    implementation libs.reactor.netty.core
    implementation libs.reactor.netty.http
    implementation libs.slf4j.api
    implementation libs.log4j.slf4j2.impl


    testImplementation testFixtures(project(":RFS"))
    testImplementation testFixtures(project(":coreUtilities"))
    testImplementation testFixtures(project(":testHelperFixtures"))
    testImplementation project(":DataGenerator")
    testImplementation project(":CreateSnapshot")
    testImplementation project(":MetadataMigration")
    testImplementation libs.lucene.v9.core
    testImplementation libs.hamcrest
    testImplementation libs.junit.jupiter.api
    testImplementation libs.junit.jupiter.params
    testImplementation libs.testcontainers.opensearch
    testImplementation libs.testcontainers
    testImplementation libs.testcontainers.toxiproxy
    testImplementation libs.mockito.core
    testImplementation libs.mockito.junit.jupiter
    testImplementation libs.json

    testRuntimeOnly libs.junit.jupiter.engine

}

application {
    mainClass.set('org.opensearch.migrations.RfsMigrateDocuments')
}

// Register copy version file task
CommonUtils.syncVersionFileToDockerStaging(project, "reindexFromSnapshot", "docker/build/version")

// Cleanup additional docker build directory
def dockerBuildDir = file("./docker/build")
clean.doFirst {
    delete dockerBuildDir
}

tasks.register('copyDockerRuntimeJars', Sync) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    description = 'Copy runtime JARs and app jar to docker build directory'

    // Define the destination directory
    def buildDir = project.file("./docker/build/runtimeJars")
    into buildDir

    // Add all the required runtime JARs to be copied
    from configurations.runtimeClasspath
    from tasks.named('jar')
    include '*.jar'

    // Explicit inputs for incremental build support
    inputs.files(configurations.runtimeClasspath)
    inputs.files(tasks.named('jar'))
    outputs.dir(buildDir)
}

DockerServiceProps[] dockerServices = [
        new DockerServiceProps([projectName:"reindexFromSnapshot",
                                dockerImageName:"reindex_from_snapshot",
                                inputDir:"./docker",
                                taskDependencies:["copyDockerRuntimeJars", "syncVersionFile_reindexFromSnapshot"]])
] as DockerServiceProps[]

for (dockerService in dockerServices) {
    tasks.register("buildDockerImage_${dockerService.projectName}", DockerBuildImage) {
        def hash = CommonUtils.calculateDockerHash(project.fileTree("docker/${dockerService.projectName}"))
        for (dep in dockerService.taskDependencies) {
            dependsOn dep
        }
        inputDir = project.file(dockerService.inputDir)
        // platform.set("linux/amd64")
        buildArgs = dockerService.buildArgs
        images.add("migrations/${dockerService.dockerImageName}:${hash}".toString())
        images.add("migrations/${dockerService.dockerImageName}:${version}".toString())
        images.add("migrations/${dockerService.dockerImageName}:latest".toString())
    }
}

tasks.register('buildDockerImages') {
    for (dockerService in dockerServices) {
        dependsOn "buildDockerImage_${dockerService.projectName}"
    }
}

tasks.named('composeUp') {
    dependsOn(tasks.named('buildDockerImages'))
}

tasks.named('slowTest') {
    testLogging.showStandardStreams = false
}
